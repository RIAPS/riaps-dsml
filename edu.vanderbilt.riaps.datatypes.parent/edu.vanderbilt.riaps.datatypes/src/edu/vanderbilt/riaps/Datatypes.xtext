/********************************************************************************/
grammar edu.vanderbilt.riaps.Datatypes with org.eclipse.xtext.common.Terminals
generate datatypes "http://www.vanderbilt.edu/riaps/Datatypes"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
// **************************************************************
// model root
Model returns Mdodel:
	{Model}
	'package' name=FQN
	(imports+=Import)*
	( typeCollections+=FTypeCollection)*
	;
// **************************************************************
// general

//Package:
//	'package' name=FQN
//;

Import :
	'import' (importedNamespace=ImportedFQN);

ImportedFQN:
	FQN ('.' '*')?;

FQN:
	ID ('.' ID)*;


	

FTypeCollection returns FTypeCollection:
	{FTypeCollection}
	'typeCollection' (name=ID)?
	'{'
		('version' version=FVersion)?
		(
			types+=FType
		)*
	'}';

FMessage returns FMessage:
	{FMessage}
	derived=[FStructType|FQN] name=ID ('key' key=[FField|FQN])?;

FVersion returns FVersion:
	{FVersion}
	'{'
		'major' major=INT
		'minor' minor=INT
    '}';
	

FTypeRef returns FTypeRef:
	predefined=FBasicTypeId | derived=[FType|FQN];

enum FBasicTypeId returns FBasicTypeId:
	Int8 = 'Int8' |
	UInt8 = 'UInt8' |
	Int16 = 'Int16' |
	UInt16 = 'UInt16' |
	Int32 = 'Int32' |
	UInt32 = 'UInt32' |
	Int64 = 'Int64' |
	UInt64 = 'UInt64' |
	Boolean = 'Boolean' |
	String = 'String' |
	Float = 'Float' |
	Double = 'Double' |
	Char = 'Char' |
	ByteBuffer = 'ByteBuffer' ;
	
	
FType returns FType:
	FEnumerationType |
	FStructType |
	FMapType ;
	
FStructType returns FStructType:
	{FStructType}
	'struct' name=ID
		(
			('extends' base=[FStructType|FQN])
		)?
	'{'
		(elements+=FField)*
    '}';

FEnumerationType returns FEnumerationType:
	{FEnumerationType}
	'enumeration' name=ID
	('extends' base=[FEnumerationType|FQN])?
	'{'
		(enumerators+=FEnumerator (','? enumerators+=FEnumerator)*)?
    '}';
    
FEnumerator returns FEnumerator:
	{FEnumerator}
	name=ID ('=' value=STRING)?
    ;
    
FMapType returns FMapType:
	{FMapType}
	'map' name=ID '{'
		keyType=FTypeRef 'to' valueType=FTypeRef
	'}';
 
FField:
	type=FTypeRef (list?='['  ']')? name=ID;
	

	