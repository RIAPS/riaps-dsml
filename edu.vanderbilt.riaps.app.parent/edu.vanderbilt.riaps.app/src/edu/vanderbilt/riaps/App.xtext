grammar edu.vanderbilt.riaps.App with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
//The compaonents and application definition use Messages from Datatypes
import "http://www.vanderbilt.edu/riaps/Datatypes" as DataTypes
generate app "http://www.vanderbilt.edu/riaps/App"
//This is the main model rule
Model returns Mdodel:
	{Model}
	'package' name=FQN
	(imports+=Import)*
	(collections+=Collection)*;

Import:
	'import' (importedNamespace=ImportedFQN) (';')?;

ImportedFQN:
	FQN ('.' '*')?;

FQN:
	ID ('.' ID)*;
	////////////////////////////////////////////////////////////////	
//These are the terminal Rules	
terminal INT returns ecore::EInt:
	'0'..'9' ('0'..'9' | '_')*;

terminal DECIMAL:
	INT
	(('e' | 'E') ('+' | '-')? INT)?
	(('b' | 'B') ('i' | 'I' | 'd' | 'D') | ('l' | 'L' | 'd' | 'D' | 'f' | 'F'))?;

FormalDefault:
	StringDefault
	| NumberDefault
	| BoolDefault;

StringDefault:
	value=STRING;

NumberDefault:
	value=Number;

BoolDefault:
	value=BOOL;

Number hidden():
	(INT | DECIMAL) ('.' (INT | DECIMAL))?;

terminal BOOL:
	'true' | 'false';

	//A collection defines the top level entries in the riaps file.
// These can just be component definitions or application definitions.

//An application is the top level
// These are the real FCO
Collection:
	Application;

	// A component can be an application component or a device component.

// This is an application component
// we capture its ports and its requirements.
// Component types used in the application
Component:
	(realtime?='rt')? 'component' name=ID
	('()' | ('(' formals+=ComponentFormal (',' formals+=ComponentFormal)* ')')?)
	'{'
	// A component can first declare several requirements. It then declares the ports that it provides.
	((constraint=ComponentUses)?)
	// A component must have atleast one port 
	(ports+=Port)+
	(handler=exceptionHandler)?
	'}';

exceptionHandler:
	IGNORE | EXIT | NAMEDHANDLER;

NAMEDHANDLER:
	{NAMEDHANDLER}
	'except' name=ID (';')?;

IGNORE:
	{IGNORE}
	'ignore' (';')?;

EXIT:
	{EXIT}
	'exit' (';')?;

//declare in us
CPURequirement:
	'cpu' percentage=INT'%'  ('over' TimeInterval_Number=INT 'us')?;

	/*
 * Rule that defines different supported time units.
 */
TimeUnit:
	seconds?='s'
	| milliseconds?='ms'
	| microseconds?='us';

terminal libraryName:
	'lib' ('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')* ('.so');
	//terminal fileName  		: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*(('.')('a'..'z'|'A'..'Z'|'_'|'0'..'9')*);
Library:
	'library' (name=libraryName);

Configuration:
	'file' (name=STRING);

	// Rate limits can be specified on ports.
Port:
	PubPort
	| SubPort
	| ClntPort
	| SrvPort
	| ReqPort
	| RepPort
	| TimPort
	| QueryPort
	| AnswerPort
	| InsPort;

QueryPort:
	'qry' name=ID ':' '(' req_type=[DataTypes::Message|FQN] ',' rep_type=[DataTypes::Message|FQN] ')' (deadline=Deadline)?
	(ratelimit=RateLimit)? (';')?;

AnswerPort:
	'ans' name=ID ':' '(' req_type=[DataTypes::Message|FQN] ',' rep_type=[DataTypes::Message|FQN] ')' (deadline=Deadline)?
	(ratelimit=RateLimit)? (';')?;

RateLimit:
	'[' (lower=INT | nolower?='-') ',' (upper=INT | noupper?='-') ']';

	//Rate limits are optional
PubPort:
	'pub' name=ID ':' type=[DataTypes::Message|FQN] (ratelimit=RateLimit)? (';')?;

SubPort:
	'sub' name=ID ':' type=[DataTypes::Message|FQN] (ratelimit=RateLimit)? (';')?;

	//deadlines can be specified for client server ports.
Deadline:
	'within' deadline=INT units=TimeUnit;

	// Client port (request and reply message types) - Owned by an SMI client
// Client operation is expected to send on the port and then receive on the same port
ClntPort:
	'clt' name=ID ':' '(' req_type=[DataTypes::Message|FQN] ',' rep_type=[DataTypes::Message|FQN] ')' (deadline=Deadline)?
	(ratelimit=RateLimit)? (';')?;

SrvPort:
	'srv' name=ID ':' '(' req_type=[DataTypes::Message|FQN] ',' rep_type=[DataTypes::Message|FQN] ')'
	(ratelimit=RateLimit)? (';')?;

ReqPort:
	'req' name=ID ':' '(' req_type=[DataTypes::Message|FQN] ',' rep_type=[DataTypes::Message|FQN] ')' (deadline=Deadline)?
	(ratelimit=RateLimit)? (';')?;

RepPort:
	'rep' name=ID ':' '(' req_type=[DataTypes::Message|FQN] ',' rep_type=[DataTypes::Message|FQN] ')'
	(ratelimit=RateLimit)? (';')?;

TimPort:
	'timer' name=ID (spec=INT (unit=TimeUnit)?) (';')?;

	//Inside port
InsPort:
	'inside' name=ID (spec?='default')? (';')?;

ComponentUses:
	{ComponentUses}
	'uses'
	'{'
	(requirements+=ComponentRequirement (';')?)+
	'}';

	/*
 * Rule that defines different supported memory units. 
 */
MemoryUnits:
	kb?='KB'
	| mb?='MB'
	| gb?="GB";

	//An application component can request a specific device. But It will need to talk via a device component running on the same 
// node that also requires the same device. Here the requirement captures a device identifier that can be specialized to 
// identify a subtag for a device for example PMU.north. In this case the device type is PMU. The north is a tag that can be hierarchically matched
// against the computation node that supports that device.
// TODO DeviceTypes might have to be defined in datatypes
ComponentRequirement:
	Library | Configuration | CPURequirement | CompDeviceRequirement | NetworkRequirement;

NetworkRequirement:
	'net' Number=INT 'MB' ('over' TimeInterval_Number=INT 'us')?;

CompDeviceRequirement:
	('device' deviceRequirement=[DataTypes::DeviceType]);
	//('tag' tag=DeviceTagSequence)?
DeviceTagSequence:
	tag+=DeviceTag ('.' tag+=DeviceTag)*;

DeviceTag:
	name=ID;

Application returns Application:
	{Application}
	'app' (name=ID)
	'{'
	//declare the libraries, actors, components (app-specific components - an app's actor can use components described in a non-app namespace)
	((actors+=Actor) | (components+=Component) | (groups+=Group))*

	//Optionally deployment constraints can be specified to control how an app is deployed.
	// the constraints are specified by the app developer
	(deploymentConstraints+=DeploymentConstraint)*
	'}';

Group:
	{Group}
	'group' name=ID '{'
	((useclauses+=UsesBlock)
	|
	(gmessageblock+=GMessageBlock)
	)+	
	'}'  
	
;

UsesBlock:
	'uses' property+=GroupProperty (',' property+=GroupProperty)*(';')?
;

GMessageBlock:
	'messages' '{'
		(('group'':' groupMessages+=[DataTypes::Message|FQN] (',' groupMessages+=[DataTypes::Message|FQN])* (';')?)
		|
		('consensus'':' consensusMessages+=[DataTypes::Message|FQN] (',' consensusMessages+=[DataTypes::Message|FQN])* (';')?)
		)+
	'}'
;	


GroupProperty:
	Leader
	| Consensus;

Leader:
	{Leader}
	hasLeader?=('leader');

Consensus:
	{Consensus}
	hasConsensus?=('consensus');

DeploymentConstraint:
	CollocateConstraint | DistributeConstraint;

CollocateConstraint:
	'collocate' actorcollocatelist+=[Actor|FQN] ',' actorcollocatelist+=[Actor|FQN] (',' actorcollocatelist+=[Actor|FQN])*
	(';'?);

DistributeConstraint:
	'distribute' actordistributelist+=[Actor|FQN] ',' actordistributelist+=[Actor|FQN] (','
	actordistributelist+=[Actor|FQN])* (';'?);

ComponentFormal:
	name=ID ('=' argDefault=FormalDefault);

ActorFormal:
	name=ID ('=' argDefault=FormalDefault);

Actor:
	'actor' name=ID ('()' | ('(' formals+=ActorFormal (',' formals+=ActorFormal)* ')')?)
	'{'
	(('local' locals+=[DataTypes::Message|FQN] (',' locals+=[DataTypes::Message|FQN])* (';')?) | ('internal'
	internals+=[DataTypes::Message|FQN] (',' internals+=[DataTypes::Message|FQN])* (';')?) | ('critical'
	criticals+=[DataTypes::Message|FQN] (',' criticals+=[DataTypes::Message|FQN])* (';')?))*
	compsection=InstanceSection
	wires+=Wire*
	((constraint=ActorUses)?)
	(handler=exceptionHandler)?
	(restartspec=RestartSpec)?
	'}';

ActorUses:
	{ActorUses}
	'uses'
	'{'
	(requirements+=ActorRequirement (';')?)*
	'}';

RestartSpec:
	{RestartSpec}
	'restart' (maxtries=INT)? ('delay' delay=INT unit=TimeUnit)? (';')?;

ActorRequirement:
	MemoryRequirement | StorageRequirement | ActorDeviceRequirement;

ActorDeviceRequirement:
	requ=CompDeviceRequirement 'tag' tagsequence=DeviceTagSequence;

MemoryRequirement:
	'mem' Number=INT 'MB'?;

StorageRequirement:
	'space' Number=INT 'MB'?;
	// Wiring of clients to servers (both SMI and AMI)
InstanceSection returns InstanceSection:
	{InstanceSection}
	'{'
	(instances+=Instance)+
	'}';

Wire returns Wire:
	{Wire}
	((clnt=[ClntPort|FQN] '=' server=[SrvPort|FQN]) | (ami?='ami' req=[ReqPort|FQN] '=' server=[SrvPort|FQN])) (';')?;

Actual returns Actual:
	{Actual} argName=[ComponentFormal|FQN] '=' (tringdefault=StringDefault | numberdefault=NumberDefault |
	boolDefault=BoolDefault | argValue=[ActorFormal|FQN]);

	//ActualValue:
//	(stringdefault=StringDefault | numberdefault=NumberDefault | boolDefault=BoolDefault | param=[ActorFormal|FQN]);
Instance:
	name=ID ':' type=[Component|FQN] ('(' actuals+=Actual (',' (actuals+=Actual))* ')')? (';')?;


	
